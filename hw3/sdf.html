<!DOCTYPE html>
<html lang="en">
  <head>
    <title>SDF</title>
    <meta charset="utf-8">
    <link rel="stylesheet" type="text/css" href="css/main.css">
  </head>
<body>
<h1 class="name">SDF By Bryce Newbury</h1>
<div id="container"></div>

<script src="js/three.js"></script>
<script src="js/OrbitControls.js"></script>

<script id="vertexShader" type="x-shader/x-vertex">
  uniform mat4 modelViewMatrix;
  uniform mat4 projectionMatrix;

  attribute vec3 position;
  
  void main() {
    gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
  }
</script>

<script id="fragmentShader" type="x-shader/x-fragment">
  // Set percision
  precision mediump float;
  precision mediump int;
  
  uniform vec2 resolution;
  uniform float time;
  
  // Globals
  const vec3 BACKGROUND_COLOR = vec3(0.0, 0.0, 0.0);
  const vec3 CAMERA_POSITION = vec3(0.0, 0.0, 5.0);
  // From https://www.shadertoy.com/view/llt3R4
  const int MAX_MARCHING_STEPS = 255;
  const float MIN_DIST = 0.0;
  const float MAX_DIST = 100.0;
  const float EPSILON = 0.0001;
  
  // Forward declarations
  vec4 rayMarch(vec3 position, vec3 targetDirection, float maxDistance);
  
  // SDFs from http://iquilezles.org/www/articles/distfunctions/distfunctions.htm
  float sdSphere( vec3 p, float s)
  {
    return length(p)-s;
  }
  float sdTorus( vec3 p, vec2 t )
  {
    vec2 q = vec2(length(p.xz)-t.x,p.y);
    return length(q)-t.y;
  }
  // Union
  float opU( float d1, float d2 )
  {
      return min(d1,d2);
  }
  // Substraction
  float opS( float d1, float d2 )
  {
      return max(-d1,d2);
  }
  // Intersection
  float opI( float d1, float d2 )
  {
      return max(d1,d2);
  }
  
  // Returns the distance to the closest object in the scene
  float scene(vec3 position) {
    // Sphere left
    float sLeft = sdSphere(position + vec3(0.6, 0.0, 0.0), 0.5);
    
    // Sphere right
    float sRight = sdSphere(position + vec3(-0.6, 0.0, 0.0), 0.5);
    
    // Combine the two spheres
    float world = opU(sLeft, sRight);
    
    // Add spheres under
    world = opU(world, sdSphere(position + vec3(0.0, 0.8, 0.0), 0.25));
    // Spheres above
    world = opU(world, sdSphere(position + vec3(0.0, -0.5, -0.1), 0.1));
    
    // Torus
    // Rotation from https://webglfundamentals.org/webgl/lessons/webgl-3d-orthographic.html
    float c = cos(time * 0.2);
    float s = sin(time * 0.2);
    // Rotate around Z
    float torus = sdTorus((vec4(position + vec3(0.0, 0.0, 0.0), 1.0) * mat4(c  ,   s, 0.0, 0.0,
                                                                     -s ,   c, 0.0, 0.0,
                                                                     0.0, 0.0, 1.0, 0.0,
                                                                     0.0, 0.0, 0.0, 1.0)).xyz,
    
                   vec2(0.75, 0.25));
    
    // Subtract the torus
    world = opS(torus, world);
    
    return world;
  }
  
  // From https://www.shadertoy.com/view/llt3R4
  vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord) {
      vec2 xy = fragCoord - size / 2.0;
      float z = size.y / tan(radians(fieldOfView) / 2.0);
      return normalize(vec3(xy, -z));
  }
  
  // Estimates the normal of the scene at position p
  vec3 getNormal(vec3 p) {
    // Sample the scene at +- EPSILON and take the difference in each direction
    return normalize(vec3(scene(vec3(p.x + EPSILON, p.yz)) - scene(vec3(p.x - EPSILON, p.yz)),
                          scene(vec3(p.x, p.y + EPSILON, p.z)) - scene(vec3(p.x, p.y - EPSILON, p.z)),
                          scene(vec3(p.xy, p.z + EPSILON)) - scene(vec3(p.xy, p.z - EPSILON))
                          ));
  }
  
  // Calculates the phong light from one light source
  // Inspired by https://www.shadertoy.com/view/lt33z7
  vec3 phongLight(vec3 position, vec3 lightPosition, vec3 lightDiffuse) {
    const vec3 lightSpecluar = vec3(0.4);
    const float specluarIntensity = 10.0;
    
    vec3 normal = getNormal(position);
    vec3 pointToLight = normalize(lightPosition - position);
    vec3 pointToCamera = normalize(CAMERA_POSITION - position);
    vec3 reflection = normalize(reflect(-pointToLight, normal));
    
    // lightSpecluar * pow(max(dot(pointToLight, normal), 0.0), specluarIntensity);
    float specular = dot(pointToLight, normal);
    float diffuse = dot(reflection, pointToCamera);
    
    if (specular < 0.0) {
      // Light invisible
      return vec3(0.0);
    }
    
    if (diffuse < 0.0) {
      // Only use defuse
      return specular * lightDiffuse;
    }
    
    return clamp(specular * lightDiffuse
                 + lightSpecluar * pow(diffuse, specluarIntensity)
                 , 0.0, 1.0);
  }
  
  // Returns the Phong color for a single light, including simple occlusion
  vec3 addLight(vec3 position, vec3 lightPosition, vec3 lightColor) {
    // Send a ray to the light, if it hits something, assume occluded
    // Need to add normalize(lightPosition) so it doesn't stop immediately,
    // by default it starts in EPSILON range
    if (rayMarch(position + normalize(lightPosition) * 0.1, lightPosition,
                 distance(lightPosition, position)).w == -1.0) {
      // Straight line to light
      return phongLight(position, lightPosition, lightColor);
    }
    
    // Is occluded
    return vec3(0.0);
  }
  
  // Returns the color of a properly lit point in space
  vec3 light(vec3 position) {
    // Ambient
    vec3 color = vec3(0.1);
    
    // Lower light
    vec3 lightPosition = vec3(0.0, -0.6, 1.5);
    vec3 lightColor = vec3(0.0 / 255.0, 171.0 / 255.0, 255.0 / 255.0) * 0.5;
    color += addLight(position, lightPosition, lightColor);
    
    // Upper light
    lightPosition = vec3(5.0 * sin(time), 3.0, 0.0);
    lightColor = vec3(255.0 / 255.0, 189.0 / 255.0, 0.0 / 255.0);
    color += addLight(position, lightPosition, lightColor);
    
    return color;
  }
  
  
  // Marches from position in direction until it hits an object
  // Returns the final position with the distance marched instead of W
  // W is -1 if maxDistance reached
  // Will stop after marching maxDistance
  vec4 rayMarch(vec3 position, vec3 targetDirection, float maxDistance) {
    vec3 direction = normalize(targetDirection);
    vec3 currentPosition = position;
    vec3 lastPosition = position;
    float lastDistance = scene(currentPosition);
    float currentDistance;
    
    for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
      // March
      currentPosition = lastPosition + direction * lastDistance;
      currentDistance = scene(currentPosition);
      
      if (distance(position, currentPosition) > maxDistance) {
        // Past end of world
        return vec4(currentPosition, -1.0);
      }
      if (currentDistance < EPSILON) {
        // Close enough to object or inside one
        return vec4(currentPosition, distance(position, currentPosition));
      }
      
      // Setup next march
      lastDistance = currentDistance;
      lastPosition = currentPosition;
    }
    
    // Return closest object when out of steps
    return vec4(currentPosition, distance(position, currentPosition));
  }
  
  // Returns the color of the closest object along the targetDirection with lightning applied
  vec3 rayMarchLit(vec3 position, vec3 targetDirection) {
    vec4 point = rayMarch(position, targetDirection, MAX_DIST);
    
    // Special case for going past the maximum distance
    if (point.w == -1.0) {
      return BACKGROUND_COLOR;
    }
    
    return light(point.xyz);
  }
  
  void main() {
    vec3 ray = rayDirection(45.0, resolution.xy, gl_FragCoord.xy);
    
    gl_FragColor = vec4(rayMarchLit(CAMERA_POSITION, ray), 1.0);
  }
</script>


<script>
  // Code based on class examples
  // Globals
  var renderer, scene, camera, uniforms, options = {};
  
  init();
  
  function init() {
    initScene();
    
    // Camera
    camera = new THREE.OrthographicCamera(window.innerWidth / - 2, window.innerWidth / 2, window.innerHeight / 2, window.innerHeight / - 2, 1, 1000);
    camera.position.z = 1;
    
    // Renderer
    renderer = new THREE.WebGLRenderer();
    renderer.setSize(window.innerWidth, window.innerHeight);
    document.getElementById('container').appendChild( renderer.domElement );
    
    //make it so that resizing the browser window also resizes the scene
    window.addEventListener( 'resize', onWindowResize, false );
    
    // Start animating
    animate();
  }
  
  function initScene() {
    // Setup scene
    scene = new THREE.Scene();
    
    // Load shaders
    var vs = document.getElementById( 'vertexShader' ).textContent;
    var fs = document.getElementById( 'fragmentShader' ).textContent;
    
    // Uniforms and GUI
    uniforms = {
      'time': {type: 'f', value: 1.0},
      'resolution': {type: 'v2', value: new THREE.Vector2(window.innerWidth, window.innerHeight)}
    }
    
    // Material
    var material = (new THREE.RawShaderMaterial({
      uniforms: uniforms,
      vertexShader: vs,
      fragmentShader: fs
    }));
    
    // Mesh
    var plane = new THREE.Mesh(new THREE.PlaneGeometry( window.innerWidth, window.innerHeight ), material);
    
    // Add to scene
    scene.add(plane);
  }
  
  function animate() {
    requestAnimationFrame(animate);
    render();
  }

  function render() {
    // Convert to seconds
    var time = performance.now() * 0.001;
    
    for (var i = 0; i < scene.children.length; i++) {
      // Update uniforms
      if (scene.children[i].type == "Mesh" &&
        scene.children[i].material.uniforms !== undefined) {
        scene.children[i].material.uniforms = uniforms;
        scene.children[i].material.uniforms.time.value = time;
      }
    }
    
    renderer.render( scene, camera );
  }
  
  function onWindowResize( event ) {
    camera.aspect = window.innerWidth / window.innerHeight;
    camera.updateProjectionMatrix();
    
    uniforms.resolution = {type: 'v2', value: new THREE.Vector2(window.innerWidth, window.innerHeight)};

    renderer.setSize( window.innerWidth, window.innerHeight );
  }

</script>

</body>
</html>

